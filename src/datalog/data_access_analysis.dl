//===- data_access_analysis.dl ------------------------------*- datalog -*-===//
//
//  Copyright (C) 2019 GrammaTech, Inc.
//
//  This code is licensed under the GNU Affero General Public License
//  as published by the Free Software Foundation, either version 3 of
//  the License, or (at your option) any later version. See the
//  LICENSE.txt file in the project root for license terms or visit
//  https://www.gnu.org/licenses/agpl.txt.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU Affero General Public License for more details.
//
//  This project is sponsored by the Office of Naval Research, One Liberty
//  Center, 875 N. Randolph Street, Arlington, VA 22203 under contract #
//  N68335-17-C-0700.  The content of the information does not necessarily
//  reflect the position or policy of the Government and no official
//  endorsement should be inferred.
//
//===----------------------------------------------------------------------===//
/**
 The data access analysis computes:

-'data_access_patterns' which represent that the address 'Address' is
 accessed with size 'Size' and multiplier 'Multiplier' from an
 instruction in 'FromWhere'.

-'preferred_data_access' the address is most likely accessed from the data access
  pattern located at ea_data_access.

data_access_patterns are computed by checking memory accesses and with
the help of the value analysis.

data_access_patterns give us very sparse information, but if an
address 'ea' is accessed with a multiplier, it is likely that
'ea'+multiplier, 'ea'+2*multiplier, etc are also accessed the same
way.  Unfortunately, we do not know until when. Therefore, we
propagate these accesses using the multiplier until we reach some
other access.
*/

// auxiliary predicate to  compute the amount of read memory
.decl instruction_memory_access_size(EA:address,Op_index:operand_index,Size:unsigned)

instruction_memory_access_size(EA,Index,SizeAccess):-
    !invalid(EA,_),
    instruction_get_operation(EA,Operation),
    instruction_get_op(EA,Index,Op),
    op_indirect(Op,_,_,_,_,_,NBytes),
    (
        // If the dest op is a register, see if there is a corresponding
        // data_access_size available.
        instruction_get_dest_op(EA,_,RegOp),
        op_regdirect(RegOp,Reg),
        arch.data_access_size(Operation,as(Reg,register),SizeAccess),
        UNUSED(NBytes)
        ;
        // If the dest op is not a register, use the operand size from the
        // instruction no matter if data_access_size is available or not.
        instruction_get_dest_op(EA,_,RegOp),
        !op_regdirect(RegOp,_),
        SizeAccess = NBytes,
        UNUSED(Operation)
        ;
        // If data_access_size is not available for the Operation,
        // use the operand size from the instruction.
        !arch.data_access_size(Operation,_,_),
        SizeAccess = NBytes
    ).

//there is a data access at EA in the operand Op_index
.decl data_access(EA:address,Op_index:operand_index,RegSegment:reg_nullable,RegBase:reg_nullable,RegIndex:reg_nullable,Mult:number,Offset:number,Size:unsigned)

data_access(EA,Op_index,RegSegment,RegBase,RegIndex,Mult,Offset,Size):-
    code(EA),
    instruction_get_op(EA,Op_index,Op),
    op_indirect_mapped(Op,RegSegment,RegBase,RegIndex,Mult,Offset,_),
    instruction_memory_access_size(EA,Op_index,Size).

// the register Reg has the given value at instruction EA and operand Op_index
.decl value_reg_at_operand(EA:address,Op_index:operand_index,Reg:reg_nullable,EA_from:address,Mult:number,Offset:number,Type:symbol)


// value_reg_at_operand_loop is just used to give priority to "loop" values over other kinds of values
.decl value_reg_at_operand_loop(EA:address,Op_index:operand_index,Reg:reg_nullable,EA_from:address,Mult:number,Offset:number,Type:symbol)


value_reg_at_operand(EA,Op_index,Reg,EA_from,Mult,BaseAddress,"loop"),
value_reg_at_operand_loop(EA,Op_index,Reg,EA_from,Mult,BaseAddress,"loop"):-
    data_access(EA,Op_index,_,Reg2,Reg3,_,_,_),
    (
        Reg = Reg2,
        UNUSED(Reg3)
        ;
        Reg = Reg3,
        UNUSED(Reg2)
    ),
    reg_nonnull(Reg,Reg_nn),
    reg_def_use.def_used(EA_def,Reg_nn,EA,Op_index),
    best_value_reg(EA_def,Reg_nn,EA_from,Mult,Offset,"loop"),
    (
        // the access is after the increment
        !reg_def_use.def_used(EA_from,Reg_nn,EA,Op_index),
        BaseAddress = Offset
        ;
        // the access is before the increment
        reg_def_use.def_used(EA_from,Reg_nn,EA,Op_index),
        BaseAddress = Offset-Mult
    ).

value_reg_at_operand(EA,Op_index,Reg,EA_from,Mult,Offset_final,Type):-
    data_access(EA,Op_index,_,Reg2,Reg3,_,_,_),
    (
        Reg = Reg2,
        UNUSED(Reg3)
        ;
        Reg = Reg3,
        UNUSED(Reg2)
    ),
    reg_nonnull(Reg,Reg_nn),
    !value_reg_at_operand_loop(EA,Op_index,Reg,_,_,_,_),
    reg_def_use.def_used(EA_def,Reg_nn,EA,Op_index),
    best_value_reg(EA_def,Reg_nn,EA_from,Mult,Offset,Type),
    (
        Offset_final = max(Offset,0)
        ;
        Type = "incomplete",
        Offset_final = 0,
        Offset = Offset // avoid warning
    ).

// case where there is not apparent definition
value_reg_at_operand(EA,Op_index,Reg,0,1,0,"incomplete"):-
    data_access(EA,Op_index,_,Reg2,Reg3,_,_,_),
    (
        Reg = Reg2,
        UNUSED(Reg3)
        ;
        Reg = Reg3,
        UNUSED(Reg2)
    ),
    reg_nonnull(Reg,Reg_nn),
    !reg_def_use.def_used(_,Reg_nn,EA,Op_index).

// NONE registers have 0 value
value_reg_at_operand(EA,Op_index,Reg,0,0,0,"complete"):-
    data_access(EA,Op_index,_,Reg2,Reg3,_,_,_),
    (
        Reg = Reg2,
        UNUSED(Reg3)
        ;
        Reg = Reg3,
        UNUSED(Reg2)
    ),
    Reg = "NONE".

/**
The address 'Address' is accessed from an instruction at address 'FromWhere'.
The access has size 'Size' and uses a multiplier 'Multiplier'.
*/
.decl data_access_pattern(Address:address,Size:unsigned,Multiplier:number,FromWhere:address)

/**
Auxiliary predicate that computes initial approximation of `data_access_pattern`
*/
.decl data_access_pattern_candidate(Address:address,Size:unsigned,Multiplier:number,FromWhere:address)

/**
The address `Address` is accessed from instruction at address `FromWhere`.
The access is done from operand `Op_index` and has size `Size`.
`simple_data_access_pattern` is a subset of `data_access_pattern_candidate`
that can be computed without using the value register
analysis.
*/
.decl simple_data_access_pattern(Address:address,Op_index:unsigned,Size:unsigned,FromWhere:address)

/**
The address at 'EA' is probably accessed with size 'Size' using the same
pattern as the data access in 'AccessEA'. Preferred data accesses are computed
by propagating data accesses based on their multiplier.
*/
.decl preferred_data_access(EA:address,Size:unsigned,AccessEA:address)

//////////////////////////////////////////////////


//single access
simple_data_access_pattern(Address,Op_index,Size,EA):-
    data_access(EA,Op_index,"NONE","NONE","NONE",_,Offset,Size),
    !instruction_has_relocation(EA,_),
    Address = as(Offset,address),
    data_segment(Beg,End),
    Address >= Beg,
    Address <= End.

// pc relative
simple_data_access_pattern(Address,Op_index,Size,EA):-
    code(EA),
    (
        pc_relative_operand(EA,Op_index,Address);
        split_load_operand(EA,Op_index,Address)
    ),
    instruction_memory_access_size(EA,Op_index,Size),
    data_segment(Beg,End),
    Address >= Beg,
    Address <= End.

// Access using relocation
simple_data_access_pattern(Address,Op_index,Size,EA):-
    data_access(EA,Op_index,_,_,_,_,_,Size),
    instruction_has_relocation(EA,EA_rel),
    symbolic_expr_from_relocation(EA_rel,_,_,_,Address),
    data_segment(Beg,End),
    Address >= Beg,
    Address <= End.

// Accesses known without value_reg computation
data_access_pattern_candidate(Address,Size,0,EA):-
    simple_data_access_pattern(Address,_,Size,EA).

// directly computed
data_access_pattern_candidate(Address,Size,Mult*Mult2,EA):-
    data_access(EA,Op_index,"NONE",RegBase,RegMult,Mult,Offset1,Size),
    !simple_data_access_pattern(_,Op_index,_,EA),
    RegMult != "NONE",
    RegMult != RegBase,
    value_reg_at_operand(EA,Op_index,RegMult,_,Mult2,Offset2,_),
    value_reg_at_operand(EA,Op_index,RegBase,_,0,Offset3,"complete"),
    // the absolute value of the multiplier cannot be
    // smaller that the size (unless we have 0 multiplier)
    (
        Mult*Mult2 > 0, as(Size,number) <= Mult*Mult2;
        Mult*Mult2 = 0;
        Mult*Mult2 < 0, as(Size,number) <= -(Mult*Mult2)
    ),
    Address = as(Offset1+(Offset2*Mult)+Offset3,address),
    data_segment(Beg,End),
    Address >= Beg,
    Address <= End.

// indirectly computed
data_access_pattern_candidate(Address,Size,Mult,EA):-
    data_access(EA,Op_index,"NONE",RegBase,RegMult,Mult2,Offset1,Size),
    !simple_data_access_pattern(_,Op_index,_,EA),
    RegMult != RegBase,
    value_reg_at_operand(EA,Op_index,RegMult,_,0,Offset2,"complete"),
    value_reg_at_operand(EA,Op_index,RegBase,_,Mult,Offset3,_),
    // the absolute value of the multiplier cannot be
    // smaller that the size (unless we have 0 multiplier)
    (
        Mult > 0, as(Size,number) <= Mult;
        Mult = 0;
        Mult < 0, as(Size,number) <= -Mult
    ),
    Address = as(Offset1+Offset2*Mult2+Offset3,address),
    data_segment(Beg,End),
    Address >= Beg,
    Address <= End.

//repeated register
data_access_pattern_candidate(Address,Size,FinalMult,EA):-
    data_access(EA,Op_index,"NONE",Reg,Reg,Mult,Offset1,Size),
    Reg != "NONE",
    value_reg_at_operand(EA,Op_index,Reg,_,Mult2,Offset2,_),
    FinalMult = Mult*Mult2+Mult2,
    (as(Size,number) <= FinalMult; FinalMult = 0),
    Address = as(Offset1+(Offset2*Mult)+Offset2, address),
    data_segment(Beg,End),
    Address >= Beg,
    Address <= End.

/**
Filter out some invalid DAP candidates before performing expensive aggregation
*/
.decl data_access_pattern_candidate_refined(Address:address,Size:unsigned,Multiplier:number,FromWhere:address)

data_access_pattern_candidate_refined(Address,Size,Multiplier,From):-
    data_access_pattern_candidate(Address,Size,Multiplier,From),
    instruction_get_operation(From,Operation),
    Operation != "LEA",
    (
        // if it is an indirect call or jump, the destination should be a potential address
        address_in_data(Address,_)
        ;
        relocation(Address,_,_,_,_,_,_)
        ;
        pe_import_entry(Address,_,_,_)
        ;
        !indirect_jump(From),
        !indirect_call(From)
    ).

/**
Unique DAP Address,Size pairs at which the max must be evaluated.

Prevents evaluating the max for each non-unique copy of the pair.
*/
.decl dap_location(Address:address,Size:unsigned)

dap_location(Address,Size):-
    data_access_pattern_candidate_refined(Address,Size,_,_).

/**
The largest multiplier for each Address,Size combination among the data access pattern candidates
*/
.decl dap_max_mult(Address:address,Size:unsigned,Max:number)

dap_max_mult(Address,Size,Max):-
    dap_location(Address,Size),
    Max = max Mult : data_access_pattern_candidate_refined(Address,Size,Mult,_).

data_access_pattern(Address,Size,Multiplier,From):-
    data_access_pattern_candidate_refined(Address,Size,Multiplier,From),
    dap_max_mult(Address,Size,Multiplier).

data_access_pattern(Address,Size,Multiplier,From):-
    data_access_pattern_candidate_refined(Address,Size,Multiplier,From),
    Multiplier = as(Size,number).

///////////////////////////////////////////////////////////////////////////////
// This code takes care of obtaining an ordering in the data_access_patterns.
// This ordering is later used to propagate data accesses.

/**
Data limits are the candidates for labeled data
and the boundaries of data segments.
*/
.decl data_access_limit(EA:address)

data_access_limit(EA):-
    labeled_data_candidate(EA);
    data_segment(_,EA);
    data_segment(EA,_).

/**
Data arrays are typically initialized with a same value.
To avoid false positives here, only consider the case where
the repeated value is 0, and the array is not singleton, and
the array size is a multiple of the access size.
*/
data_access_limit(EA):-
    data_access_pattern_non_zero(EA_labeled,Size,Size,_),
    repeated_byte(EA0,0,Count),
    EA = EA0 + Count,
    EA_labeled >= EA0,
    EA_labeled < EA,
    Len = EA - EA_labeled,
    Len > Size * 2,
    Len % Size = 0.

/**
Labeled data boundaries
*/
.decl data_limit(EA:address)

data_limit(EA):-
    labeled_data_candidate(EA);
    data_segment(_,EA);
    data_segment(EA,_).

/**
The next data limit after 'EA' is at 'Next'.
*/
.decl next_data_limit(EA:address,Next:address)

next_data_limit(EA,Next):-
    data_limit(EA),
    data_segment(Beg,End), EA >= Beg, EA < End,
    Next = min Next : {data_limit(Next), Next > EA},
    Next <= End.

/**
The first data limit after the data access at address
'Access' is at address 'DataLimit'.
*/
.decl data_limit_after_access(Access:address,DataLimit:address)


data_limit_after_access(Access,DataLimit):-
    data_access_pattern(Access,_,_,_),
    data_segment(Beg,End), Access >= Beg, Access < End,
    DataLimit = min DataLimit : {data_access_limit(DataLimit), DataLimit > Access},
    DataLimit <= End.

/**
The next data access after the one at 'EA' is at 'Next'.
*/
.decl next_data_access(EA:address,Next:address)

next_data_access(EA,Next):-
    data_access_pattern(EA,_,_,_),
    data_segment(Beg,End), EA >= Beg, EA < End,
    Next = min Next : {data_access_pattern(Next,_,_,_), Next > EA},
    Next <= End.


// FIXME: This predicate has been added to avoid exceptions when mult is 0
// due to https://github.com/souffle-lang/souffle/issues/1513
.decl data_access_pattern_non_zero(Address:address,Size:unsigned,Multiplier:unsigned,FromWhere:address)

data_access_pattern_non_zero(Address,Size,as(Mult,unsigned),FromWhere):-
    data_access_pattern(Address,Size,Mult,FromWhere), Mult > 0.


/**
Barriers are the candidates for limiting synchronous access pairing.
*/
.decl synchronous_access_barrier(EA:address)

synchronous_access_barrier(EA):-
    symbol(EA,_,_,_,_,_,_,_,_);
    data_segment(_,EA);
    data_segment(EA,_).

/**
The first synchronous_access barrier after the data access to 'EA' is at
address 'Barrier'.
*/
.decl synchronous_access_barrier_after_access(EA:address,Barrier:address)

synchronous_access_barrier_after_access(EA,Barrier):-
    data_access_pattern(EA,_,_,_),
    Barrier = min Barrier : {synchronous_access_barrier(Barrier), Barrier > EA}.


/**
Two data accesses are synchronous if:
1. The second access has multiplier 0 and it originates from the same address
  as the first.
2. Or, They have the same multiplier and the space between them
  means they do not collide with each other even if they are propagated.

`synchronous_access` denotes that the access at `RefAccess` is synchronous
with all the accesses from `RefAccess` to `LastEA`.
For example, if we have the consecutive accesses: A, B, C, D.
synchronous_access(A,D) means that A is synchronous with B, C and D.

Note that if synchronous_access(A,D) is generated, so will
synchronous_access(A,B) and synchronous_access(A,C) since
the computation is incremental.
We could use subsumption to remove those intermediate results
but that would require reworking `first_synchronous_access`
as well.
*/
.decl synchronous_access(RefAccess:address,LastEA:address)

synchronous_access(EA,EA2):-
    data_access_pattern_non_zero(EA,Size,Mult,From),
    next_data_access(EA,EA2),
    data_access_pattern(EA2,Size2,Mult2_s,From2),
    synchronous_access_barrier_after_access(EA,Barrier),
    EA2 < Barrier,
    Mult2 = as(Mult2_s,unsigned),

    Rem = (EA2 - EA) % Mult,
    Size <= Rem,
    Rem + Size2 <= Mult,
    (
        Mult2 = 0,
        From2 = From,
        Size2 = Size,
        Rem = 0
        ;
        Mult2 = Mult
    ).

synchronous_access(EA,EA2):-
    synchronous_access(EA,EA_inter),
    data_access_pattern_non_zero(EA,Size,Mult,From),
    next_data_access(EA_inter,EA2),
    data_access_pattern(EA2,Size2,Mult2_s,From2),
    synchronous_access_barrier_after_access(EA_inter,Barrier),
    EA2 < Barrier,
    Mult2 = as(Mult2_s,unsigned),

    Rem = (EA2 - EA) % Mult,
    Size <= Rem,
    Rem + Size2 <= Mult,
    (
        Mult2 = 0,
        From2 = From,
        Size2 = Size,
        Rem = 0
        ;
        Mult2 = Mult
    ).

/**
The first synchronous data access of 'Access' is located
at address 'Ref'.
*/
.decl first_synchronous_access(Ref:address,Access:address)

first_synchronous_access(Ref,Access):-
    synchronous_access(_,Access),
    Ref = min Ref: {synchronous_access(Ref,Access)}.


/**
The next data access after 'EA' that is not synchronous with 'EA'
is located at address 'Next'.
*/
.decl next_dyssynchronous_data_access(EA:address,Next:address)

next_dyssynchronous_data_access(EA,Next):-
    data_access_pattern(EA,_,_,_),
    (
        synchronous_access(EA,End),
        next_data_access(End,Next),
        !synchronous_access(EA,Next)
        ;
        !synchronous_access(EA,_),
        next_data_access(EA,Next)
    ).


/////////////////////////////////////////////
// The actual propagation of data accesses

preferred_data_access(EA,Size,EA):-
    data_access_pattern(EA,Size,0,_),
    data_byte(EA,_).


// Data accesses with non-zero multiplier are propagated until the next
// dysynchronous data access or the next data limit, whichever comes first.
// This propagation is not recursive, it uses range to iterate.
preferred_data_access(EA_prop,Size,EA_access):-
    data_access_pattern_non_zero(EA_access,Size,Mult,_),
    data_byte(EA_access,_),
    (
        next_dyssynchronous_data_access(EA_access,Next_access),
        Limit = min(Next_access,Next_limit)
        ;
        !next_dyssynchronous_data_access(EA_access,_),
        Limit = Next_limit
    ),
    data_limit_after_access(EA_access,Next_limit),
    EA_prop = range(EA_access,Limit,Mult).
